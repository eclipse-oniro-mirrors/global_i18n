# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/global/i18n/i18n.gni")
import("//build/ohos.gni")

group("build_module") {
  deps = [
    ":cj_i18n_ffi",
    "intl:cj_intl_ffi",
  ]
}

ohos_shared_library("cj_i18n_ffi") {
  include_dirs = [
    "../../frameworks/intl/include",
    "include",
  ]

  if (!defined(defines)) {
    defines = []
  }

  if (!build_ohos_sdk) {
    deps = [
      "../../frameworks/intl:intl_util",
      "../../frameworks/intl:preferred_language",
    ]
    external_deps = [
      "c_utils:utils",
      "common_event_service:cesfwk_innerkits",
      "hilog:libhilog",
      "icu:shared_icui18n",
      "icu:shared_icuuc",
      "ipc:ipc_core",
      "libphonenumber:phonenumber_standard",
      "libpng:libpng",
      "libxml2:libxml2",
      "napi:ace_napi",
      "napi:cj_bind_ffi",
      "napi:cj_bind_native",
      "preferences:native_preferences",
      "samgr:samgr_proxy",
    ]
    sources = [
      "src/i18n_breakIterator_ffi.cpp",
      "src/i18n_breakIterator_impl.cpp",
      "src/i18n_calendar_ffi.cpp",
      "src/i18n_calendar_impl.cpp",
      "src/i18n_entity_ffi.cpp",
      "src/i18n_entity_impl.cpp",
      "src/i18n_ffi.cpp",
      "src/i18n_holiday_ffi.cpp",
      "src/i18n_holiday_impl.cpp",
      "src/i18n_index_util_ffi.cpp",
      "src/i18n_index_util_impl.cpp",
      "src/i18n_normalizer_ffi.cpp",
      "src/i18n_normalizer_impl.cpp",
      "src/i18n_phone_number_format_ffi.cpp",
      "src/i18n_phone_number_format_impl.cpp",
      "src/i18n_system_ffi.cpp",
      "src/i18n_timezone_ffi.cpp",
      "src/i18n_timezone_impl.cpp",
      "src/i18n_transliterator_ffi.cpp",
      "src/i18n_transliterator_impl.cpp",
      "src/i18n_unicode_ffi.cpp",
      "src/i18n_util_ffi.cpp",
    ]
  } else {
    defines += [ "PREVIEWER" ]
    sources = [ "cj_i18n_mock.cpp" ]
    external_deps = [ "napi:cj_bind_ffi" ]
  }

  if (current_os == "ohos") {
    defines += [ "OHOS_PLATFORM" ]
  }

  if (current_os == "mingw") {
    defines += [ "WINDOWS_PLATFORM" ]
  }

  if (i18n_support_app_preferred_language) {
    defines += [ "SUPPORT_APP_PREFERRED_LANGUAGE" ]
  }

  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "global"
  part_name = "i18n"
}
